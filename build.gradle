plugins {
    id 'org.springframework.boot' version '2.5.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.asciidoctor.convert' version '1.5.8'
    id 'java'
}

group = 'com.postsquad'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
    set('mapstructVersion', "1.4.2.Final")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.boot:spring-boot-starter-quartz'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.3'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'mysql:mysql-connector-java'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'com.google.guava:guava:16+'
    implementation 'com.nimbusds:nimbus-jose-jwt'

    /* MapStruct
    * org.mapstruct:mapstruct-processor가 Lombok보다 위에 있어야 제대로 작동합니다.
    * 그렇지 않으면 빌더 인식이 제대로 되지 않을 수 있습니다.
    * 참고
    * - https://github.com/projectlombok/lombok/issues/1538
    * - https://github.com/mapstruct/mapstruct/issues/1581
    * - https://github.com/mapstruct/mapstruct/issues/1997
    */
    implementation 'org.mapstruct:mapstruct' + ":$mapstructVersion"
    annotationProcessor 'org.mapstruct:mapstruct-processor' + ":$mapstructVersion"

    /* Lombok */
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor  'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    /* Swagger */
    implementation group: 'io.springfox', name: 'springfox-swagger2', version: '2.9.2'
    implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.9.2'
    implementation group: 'org.springframework.plugin', name: 'spring-plugin-core', version: '1.2.0.RELEASE'

    /* Querydsl */
    implementation group: 'com.querydsl', name: 'querydsl-apt', version: '4.4.0'
    implementation group: 'com.querydsl', name: 'querydsl-jpa', version: '4.4.0'
    annotationProcessor group: 'com.querydsl', name: 'querydsl-apt', classifier:'jpa', version: '4.4.0'
    annotationProcessor group: 'jakarta.annotation', name: 'jakarta.annotation-api', version: '1.3.5'
    annotationProcessor group: 'jakarta.persistence', name: 'jakarta.persistence-api', version: '2.2.3'
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
}

jar {
    enabled = false
}
